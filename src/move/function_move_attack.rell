

// --------------------------------------------------------------
// The player's client reports an attack.
// --------------------------------------------------------------
function move_attack_inner(
    caller_pubkey: pubkey,
    m_args: move_arguments,

    att_args: move_attack_arguments,


    // ----------------------
    // Needed for chaos mode
    // ----------------------

    // ----------------------
    // Random 
    // If any randomness was used while attacking, the numbers generated goes in here.
    // The list can have different length:
    // 1. classical mode = 0
    // 2. chaos mode normal attack: 2 die rolls or sometimes 3 die rolls if miss, 
    // 3. chaos mode retaliation: 4 die rolls.
    // ----------------------
    rnd_numbers: list<rnd_data>, 

    check_args: move_check_arguments? = null,

    // ----------------------
    // Some args
    // ----------------------
    chaos_args: move_chaos_mode_arguments? = null,

    // Only used if there is retaliation.
    ret_args: move_retaliation_arguments? = null

) {
    // ----------------------------------------------    
    // A. Verification
    // ----------------------------------------------    
    // No need to do any updates before we have verified everything.
    // (But Chromia will roll back any updates if we get an error, so it's not critical that we do it this way.)

    var dbg = move_debug_str(m_args, check_args);
    val p_dbg = "type: " + m_args.piece_type_str + " (from: " + m_args.from_position + " -> attack on: " + att_args.attacked_position + " -> dest " + m_args.to_position + ")";

    val game_id = m_args.game_id;
    log("Move Attack Begin: game_id: " + game_id + ", last_turn: " + m_args.last_seen_turn, ", piece " + p_dbg + ", flags: " + dbg);
    
    // ----------------
    // A.1. Check player entity
    // ----------------
    //log("Move A.1");
    val minimal_game: minimal_game_result? = get_minimal_game_data(caller_pubkey);
    require (minimal_game != null, "Player is not in a game, id:" + game_id);

    // ----------------
    // A.2. Is game is out of sync? 
    // ----------------
    //log("Move A.2");
    val curr_turn = minimal_game.curr_turn_nr;
    require(curr_turn == m_args.last_seen_turn, "Player client is on turn " + m_args.last_seen_turn + " but blockchain is on turn " + curr_turn);

    // ----------------
    // A.3. Validate positions
    // ----------------
    //log("Move A.3");
    move_verify_pos(game_id, m_args);

    // ----------------
    // A.4. Verify attacker piece type and position
    // ----------------
    //log("Move A.4: Attacker" + piece_type_str + " at " + from_position);
    val v_res = move_verify_basic_move(game_id, m_args, minimal_game);
    val p_type = v_res.p_type; // The main piece type
    val p = v_res.p; // The main piece data
    val align = v_res.align; // This players color
    val opp_align = v_res.opp_align; // Opponent's color

    // ----------------
    // A.5 Verify opponent's piece
    // ----------------
    // Get the opponent's vulnerable positions if any
    val res_v_path = move_get_vuln_path(minimal_game.game, opp_align); 
    val opp_vuln_ctxt: pre_vuln_context = res_v_path.v_ctxt;
    val v_path_opp_piece: piece? = res_v_path.moved_piece;

    val (opp_vuln: vulnerable_data, opp: piece_living_data?) = move_verify_opponent_piece(game_id, align, att_args, minimal_game, opp_vuln_ctxt, v_path_opp_piece);
  
    // ----------------
    // A.6 Battle calculation
    // ----------------
    val is_classic_mode: boolean = game_is_classic(minimal_game.game_type); 
    val is_chaos_mode: boolean = not is_classic_mode; // Else it's chaos mode.

    // After this we know that our calculation matches the given data.
    val battle_res = move_calc_and_verify_battle_result(
        game_id = game_id,
        m_args = m_args,
        att_args = att_args,
        p = p,
        opp_vuln = opp_vuln,
        rnd_numbers = rnd_numbers,
        is_chaos_mode = is_chaos_mode,
        chaos_args = chaos_args,
        ret_args = ret_args
    );

    // ----------------
    // A.6 Get same state
    // ----------------
    // Has this piece an jump enchantment on it?
    val is_enchanted_jump = move_has_enchanted_jump(minimal_game.player_game, p.p);

    // ----------------
    // A.6. Verify if move is legal
    // ----------------

    // Complex verification (still easier than check mate).
    var m_tree: move_tree? = null;


    val board: list<with_pos_and_alignment> = piece_get_all_vulnerables(minimal_game.game); 
    val portals: list<owned_portal> = move_get_all_portals(minimal_game.player_game, align);
    val board_positions_all = board_positions_all_constructor(minimal_game.game, board, portals);


    //log ("Move A.6.1 - calc move trees ");
    val (ground_m_tree: move_tree?, jump_m_tree: move_tree?) = rules_get_all_valid_moves(
            board_positions_all, 
            p_type,
            is_enchanted_jump,
            p.is_knighted,
            m_args.from_position,
            align,
            opp_vuln_ctxt
    );

    //log ("Move A.6.2 - move trees done ");
    require(ground_m_tree != null or jump_m_tree != null, "No move tree found, cannot move piece " + p_dbg + ", id:" + game_id);
    m_tree = move_tree_merge(ground_m_tree, jump_m_tree);
    require(m_tree != null, "Failed merge?");

    // Note: 
    // The move that must exist in the move tree is the "attack_position", 
    // the "to_position" can be anything, but must be empty.
    val found_move = move_tree_get_pos_status(m_tree, att_args.attacked_position);
    require(found_move > 0, "Illegal move: piece " + p_dbg + ", id:" + game_id);
    require(found_move == 2, "Expected an attacking move, but on piece on attacked position, piece " + p_dbg + ", id:" + game_id);

    val selected_path = move_tree_build_active_path_from_destination(m_tree, m_args.to_position);
    require(selected_path != null, "Failed to build active path from destination: " + p_dbg + ", id:" + game_id);

    // Get all pieces of both teams
    // Expensive, so we wait until last moment
    val our_team = team_build_from_db(minimal_game.game, align);
    val opp_team = team_build_from_db(minimal_game.game, opp_align);

    val our_king = team_get_king(our_team)!!;
    val opp_king = team_get_king(our_team)!!;

    // ----------------
    // A.7. Verify if this move will get us into check
    // ----------------
    //log ("Move A.7 - Verify if this move will get us into check ");
    val p_wpaa = with_pos_and_alignment(m_args.from_position, align);
    val get_into_check = will_hypothetical_move_cause_check(
            selected_path,
            p_wpaa,
            align,
            our_king.vuln.position,
            opp_team,
            board_positions_all
    );

    // The client should have stopped this move from happening.
    require(not get_into_check, "Move puts us into check: " + p_dbg + ", id:" + game_id);


    log("Move Attack passed verification, piece " + p_dbg + ", flags: " + dbg + ", game_id: " + game_id);

    // ----------------------------------------------    
    // B. Updates 
    // ----------------------------------------------    
    var new_turn_type = turn_type.BLACK_MOVE;
    if (minimal_game.is_white) { 
        new_turn_type = turn_type.WHITE_MOVE;
    }
    // ----------------
    // Update piece position
    // ----------------

    // B.1 Do this FIRST since lots of verification in here.
    val new_turn = turn_create(minimal_game.game, minimal_game.player_game, new_turn_type, minimal_game.is_white);

    // B.2 Update hp and is_alive for both attacker and defender
    // NOTE: Must be done BEFORE we move the attacker, since we require both combatants to stay on original positions.
    val attacker_still_alive = move_battle_update_health(battle_res, is_chaos_mode, p, opp_vuln, board_positions_all);

    if (attacker_still_alive) {
        // B.3 Same as for peaceful move
        move_common_change_piece_position(board_positions_all, p.p.vulnerable, p_wpaa, m_args.from_position, m_args.to_position);
    }

    // B.4 Use all unblocked portals
    move_common_use_all_unblocked_portals(minimal_game.game, new_turn, board_positions_all, opp_team, align);

    // B.5 Promotions
    // TODO

    // B.6 Verify if we just won via checkmate?
    val (is_check, is_checkmate, is_stalemate) = move_common_check_stale_n_mate(minimal_game, board_positions_all, opp_align, opp_king, our_team, opp_team, check_args);

    // B.7 Turn and static move
    val is_castle = false; // Must be false for attacks
    val m = move_event_creation(m_args, minimal_game, new_turn, p.p, is_castle, is_check); 

    // ----------------
    // B.8. Create attack_move entity
    // ----------------

    val am = create attack_move(
        move = m,
        attacked_position = att_args.attacked_position,
        result_type = battle_res.result,
        defender_defense_points = att_args.defender_defense_points,
        damage_points = att_args.damage_points
    );

    if (is_chaos_mode) {

        // B.8.chaos.1. Create chaos_mode_args entity
        require(chaos_args != null, "Internal error: Chaos mode args is null, id:" + game_id); // Already checked above.
        create chaos_mode_args(
            attack_move = am,
            defender_can_retaliate = chaos_args.defender_can_retaliate,
            is_flash_attack = chaos_args.is_flash_attack,
            is_luck = chaos_args.is_luck 
        );

        when (battle_res.result) {
            battle_result_type.HIT -> {
                // B.8.chaos.2. Create retaliation entity
                require(ret_args != null, "Internal error: Retaliation args is null, id:" + game_id); // Already checked above.
                val retaliation_move = create retaliation(
                    attack_move = am,
                    result_type = battle_res.retaliation_result,
                    attacker_defence_points = ret_args.attacker_defence_points,
                    damage_points = ret_args.damage_points
                );
            }
        }

        // ----------------
        // B.8.chaos.3. Save the rnd results
        // ----------------
        rnd_validation_create_player_rnd_nr(new_turn, battle_res.rnd_results);
    }

    // ----------------
    // B.9 Is it over?
    // ----------------
    if (is_checkmate or is_stalemate) {
        log("Game Over");
        if (is_checkmate) {
            update minimal_game.game (.current_turn = new_turn.number, .is_finished = true, .is_draw = false);

            // TODO
        } else {
            // Stalemate
            update minimal_game.game (.current_turn = new_turn.number, .is_finished = true, .is_draw = true);

            // TODO
        }

    } else {
        // Cleanup
        move_cleanup_after_move(is_enchanted_jump, opp_vuln_ctxt, minimal_game, align);
        // Won't add any vulns for attacks.

        update minimal_game.game (.current_turn = new_turn.number);
    }

    log("/Move Attack End, new turn type: " + new_turn_type + ", piece " + p_dbg + ", flags: " + dbg + ", game_id: " + game_id);
}

