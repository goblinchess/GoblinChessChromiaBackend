
// -------------------------------------
// Some definitions:
// - A "color rotation" is everything that happens during black's or white's "turn".
// - A color rotation has 3 "turns", which is the smallest unity of time in the game.
//   When a player "does something", it will generate a new "turn".
// -------------------------------------

// Calculates the maximum turn number for a color rotation
function color_rotation_find_max_turn_nr(
    last_seen_turn_nr: integer,
): integer {

    // If he [last_seen_turn_nr] is the last turn of a color rotation, we'll look at the the next color rotation.
    val color_rotation_nr = last_seen_turn_nr / 3;
    return color_rotation_nr * 3 + 3;
}

// -------------------------------------
// Get the color rotation
//
// This function will return all actions (all turns) in the same "color rotation"  
// that came after the given [last_seen_turn]. 
// 
// Note on performance:
// To speed things up, we want to return everything the client needs in one single query (all "turns").
// This way we can minimize the number of DB roundtrips.
// -------------------------------------
function color_rotation_get_all_actions(
    game_id: integer, 
    minimal_game: minimal_game_result, 
    last_seen_turn: integer,
    otd: opponent_timeout_data
): color_rotation_actions? {

    val max_turn_nr_of_the_color_rotation = color_rotation_find_max_turn_nr(last_seen_turn);

    val recent_turns = (t: turn) @* {
        t.created_by == minimal_game.player_game, 
        t.number > last_seen_turn,
        t.number <= max_turn_nr_of_the_color_rotation}  
        (@sort nr = t.number, t.turn_type, trn = t);
    
    var turns_found = recent_turns.size();
    if (turns_found == 0) { 
        // Error, since we wan't to do minimal SELECTs.
        log("game_id " + game_id + "ERROR: Don't call this function unless there is something to fetch!");
        return null; 
    }

    // The idea is that this variable will be updated without gaps, else sorting is broken.
    var return_turn_nr: integer = last_seen_turn;  // The new last seen turn nr
    var rotate_color: boolean = false;

    // If we have a non-skipped turn, these will get filled with data
    var card_played_data: card_played_data? = null;
    var primary_move: move_full_data? = null;
    var extra_move: move_full_data? = null;

    // We go through all found new turns and act depending on the [turn_type].
    for(recent_turn in recent_turns) {

        when (recent_turn.turn_type) {
            turn_type.WHITE_CARD_SKIPPED,
            turn_type.BLACK_CARD_SKIPPED -> {
                // No card was played. All we do is count up the turn.
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Card played should be first thing seen, last seen turn was " + return_turn_nr + ", but card has turn nr: " + recent_turn.nr);
                }
                return_turn_nr = recent_turn.nr; 
            }
            turn_type.WHITE_CARD,
            turn_type.BLACK_CARD -> {
                // A card was played, we must gather info 
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Card played should be first thing seen, last seen turn was " + return_turn_nr + ", but card has turn nr: " + recent_turn.nr);
                }
                card_played_data = card_played_data_get(recent_turn.trn, recent_turn.turn_type, recent_turn.nr);
                return_turn_nr = recent_turn.nr;
            }
            turn_type.WHITE_MOVE_SKIPPED, 
            turn_type.BLACK_MOVE_SKIPPED -> {
                // Even skipped moves can have a lot of events on them.
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Something wrong with the order, last seen turn was " + return_turn_nr + ", but move has turn nr: " + recent_turn.nr);
                }
                return_turn_nr = recent_turn.nr; 

                // A move was played, we must gather info
                primary_move = move_full_data_get(
                    game_id = game_id,  
                    is_skipped_move = true, 
                    t = recent_turn.trn, 
                    turn_type = recent_turn.turn_type, 
                    turn_nr = recent_turn.nr, 
                    minimal_game = minimal_game
                );

            }
            turn_type.WHITE_MOVE, 
            turn_type.BLACK_MOVE -> {
                // Regular move
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Something wrong with the order, last seen turn was " + return_turn_nr + ", but move has turn nr: " + recent_turn.nr);
                }
                return_turn_nr = recent_turn.nr; 

                // A move was played, we must gather info
                primary_move = move_full_data_get(
                    game_id = game_id,  
                    is_skipped_move = false, 
                    t = recent_turn.trn, 
                    turn_type = recent_turn.turn_type, 
                    turn_nr = recent_turn.nr, 
                    minimal_game = minimal_game
                );
            }
            turn_type.NEUTRAL_MOVE_1_SKIPPED, 
            turn_type.NEUTRAL_MOVE_2_SKIPPED -> {
                // Typical case.
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Something wrong with the order, last seen turn was " + return_turn_nr + ", but move has turn nr: " + recent_turn.nr);
                }
                return_turn_nr = recent_turn.nr; // Kind of unusal that the opponent would send only the skipped turn, but yeah.
                rotate_color = true;
                extra_move = null; // Nothing happenend! 
            }
            turn_type.NEUTRAL_MOVE_1_HIJACKED, 
            turn_type.NEUTRAL_MOVE_2_HIJACKED -> {
                // Double move.
                if(recent_turn.nr != return_turn_nr + 1) 
                { 
                    log("game_id " + game_id + "ERROR: Something wrong with the order, last seen turn was " + return_turn_nr + ", but move has turn nr: " + recent_turn.nr);
                }
                return_turn_nr = recent_turn.nr; // Kind of unusal that the opponent would send only the skipped turn, but yeah.
                rotate_color = true;

                // This is just one extra normal move.
                // We fetch the data without knowing it's actually a rather special thing.
                extra_move = move_full_data_get(
                    game_id = game_id,  
                    is_skipped_move = true, 
                    t = recent_turn.trn, 
                    turn_type = recent_turn.turn_type, 
                    turn_nr = recent_turn.nr, 
                    minimal_game = minimal_game
                );
            }
            turn_type.NEUTRAL_MOVE_1, 
            turn_type.NEUTRAL_MOVE_2 -> {
                log("game_id " + game_id + "ERROR: Not used in v1.0.");
            }
            else -> {
                log("game_id " + game_id + "ERROR: Unknown turn type: " + recent_turn.turn_type);
            }
        }

    }

    // Some sanity checks.
    if (return_turn_nr > max_turn_nr_of_the_color_rotation) {
        log("game_id " + game_id + "ERROR: return_turn_nr > max_turn_nr, return_turn_nr: " + return_turn_nr + ", max_turn_nr: " + max_turn_nr_of_the_color_rotation);
    }
    if (return_turn_nr == max_turn_nr_of_the_color_rotation and not rotate_color) {
        log("game_id " + game_id + "ERROR: return_turn_nr == max_turn_nr and not rotate_color, return_turn_nr: " + return_turn_nr + ", max_turn_nr: " + max_turn_nr_of_the_color_rotation);
    }

    // Get current time
    // Note: it's hard to get the current time from within a query, 
    // since we cannot use [op_context.last_block_time] here.
    var curr_time_ms: timestamp = 0; 
    val maybe_curr_time_ms = block @? {} (ts = .timestamp, @omit @sort_desc .block_height ) limit 1;
    if (maybe_curr_time_ms != null) { // During automated testing there are no blocks, so we cannot "require()" here
        curr_time_ms = maybe_curr_time_ms.ts;
    }

    // It's convenient that the node returns information about opponent's timeout status.
    val opp_timeout: boolean = game_timeout_calc_core(game_id, curr_time_ms, otd);

    val ti = game_timeout_build_timeout_info(opp_timeout, otd, minimal_game.total_time_left_since_color_rotation_ms, curr_time_ms, minimal_game.game_created_at);

    return color_rotation_actions(
        curr_turn_nr = return_turn_nr, // <- Should be the last seen turn, if our @sort works
        rotate_color = rotate_color,
        card_played_data = card_played_data,
        primary_move = primary_move,
        extra_move = extra_move, 
        timeout_info = ti
    );

} 